//TESTING

// set up a neutral nonWF simulation
initialize() {
	initializeSLiMModelType("nonWF");
	initializeSLiMOptions(keepPedigrees=T);
	initializeTreeSeq();
	
	if(exists("slimgui")) { // now K and B are defined from the CL
		defineConstant("K", 100);	// carrying capacity
		defineConstant("B", 100); // bottleneck carrying capacity
		defineConstant("O", "./dc_slim.trees"); // tree file
		defineConstant("M", "./meta_dc_slim.txt"); // metadata file
	}
	
	initializeMutationType("m1", 0.5, "f", 0.0); // neutral mutations, which are allowed to fix

	
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, 9999999); // should be 2589999999
	initializeMutationRate(0); // mutations will be added in msprime
	initializeRecombinationRate(1e-8);
	
	// set up output
	HeaderLine = "generation\tindividual_id\tage";
	writeFile(M,HeaderLine,append=T);
}

reproduction(NULL) {
	
	if(sim.generation <=33999){
		
		if (individual.age > 0) {
			mate = subpop.sampleIndividuals(1, minAge=1);
			prob_offspring = min(1.0, (K/p1.individualCount));
			offspring_count=rbinom(1, 1, prob_offspring);
			for (i in seqLen(offspring_count)){
				subpop.addCrossed(individual, mate);
			}
		}
	}
	
	if(sim.generation > 33999){
		
		if (individual.age > 0) {
			mate = subpop.sampleIndividuals(1, minAge=1);
			prob_offspring = min(1.0, (B/p1.individualCount));
			offspring_count=rbinom(1, 1, prob_offspring);
			for (i in seqLen(2*offspring_count)){
				subpop.addCrossed(individual, mate);
			}
		}
	}
}

// create an initial population of K individuals
1 early() {
	sim.addSubpop("p1", K);
	p1.individuals.age = rdunif(K, min=0, max=39);
}

// pre-bottleneck fitness scaling
1:33999 early() {
	// density-dependence
	p1.fitnessScaling = K / (p1.individualCount);
}

// post-bottleneck fitness scaling
// demographic decline starts at time 34000
34000:35000 early() {
	// density-dependence
	p1.fitnessScaling = B / (p1.individualCount);
}

// remember individuals every ten years for 1000 years

// before decline
33800:33999 late(){
	if (sim.generation % 10 == 0){ //every ten generations
		catn("Generation " + sim.generation + ": " + p1.individualCount);
		sample = p1.sampleIndividuals(100, replace = F);
		sim.treeSeqRememberIndividuals(sample);
		for(i in 0:(length(sample)-1)){ //remember metadata for each individual at this point in time (will be overwritten in the treesequence metadata) 
			OutputLine  = paste(sim.generation, p1.individuals.pedigreeID[i], p1.individuals[i].age, sep = "\t");
			writeFile(M, OutputLine, append=T);
		}
	}
}

// after decline
34000:35000 late(){
	if (sim.generation % 10 == 0){ //every ten generations
		catn("Generation " + sim.generation + ": " + p1.individualCount);
		sim.treeSeqRememberIndividuals(p1.individuals);
		for(i in 0:(p1.individualCount-1)){ //remember metadata for each individual at this point in time (will be overwritten in the treesequence metadata) 
			OutputLine  = paste(sim.generation, p1.individuals.pedigreeID[i], p1.individuals[i].age, sep = "\t");
			writeFile(M, OutputLine, append=T);
		}
	}
}

35000 late() {
	sim.treeSeqOutput(O);
} 
